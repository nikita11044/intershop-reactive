plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management'
    id 'org.openapi.generator' version '7.4.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // Redis Reactive
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'

    // Cache
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // Liquibase
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.springframework:spring-jdbc'

    // PostgreSQL
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'

    // MinIO
    implementation 'io.minio:minio:8.5.7'

    // Lombok & MapStruct
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Thymeleaf
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:r2dbc'
}

// OpenAPI
openApiGenerate {
    generatorName = "java"
    inputSpec = "$projectDir/src/main/resources/payment-openapi.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "practicum.intershopreactive.api"
    modelPackage = "practicum.intershopreactive.model"
    invokerPackage = "practicum.intershopreactive.invoker"

    generateApiTests = false
    generateModelTests = false

    configOptions = [
            library              : "webclient",
            useJakartaEe         : "true",
            openApiNullable      : "false",
            hideGenerationTimestamp: "true",
            useTags              : "true",
            serializableModel    : "true",
            dateLibrary          : "java8"
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate
